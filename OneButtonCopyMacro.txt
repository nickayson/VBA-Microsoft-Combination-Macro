Sub CopyRangeFromMultiSheets()

    'Check if a Destination sheet exists with function call, creates a new one if false
    If SheetExists("Results") = False Then
        Sheets.Add
        ActiveSheet.name = "Results"
    End If
    
    Dim sh As Worksheet
    Dim DestSh As Worksheet
    Dim FirstSh As Worksheet
    Dim LastRow As Long
    Dim CopyRng As Range

    With Application
        .ScreenUpdating = False
        .EnableEvents = False
        
        'Destination sheet as results
        Set DestSh = Sheets("Results")
        'FirstSheet is detected at first sheet in the workbook
        Set FirstSh = Sheets(1)
        
        'iterate through all worksheets in workbook
        For Each sh In ActiveWorkbook.Worksheets
            Select Case sh.name
            
            'Ignore Destination Sheet
            Case DestSh.name
            
            'Will only run 1 time to copy the first header of the first document
            Case FirstSh.name
            'Copy from worksheet until a cell in the column A is empty
            'It will then paste to Results sheet at the end of existing data
                LastRow = DestSh.Cells(Rows.Count, 1).End(3).Row + 1
                sh.Range("A1", sh.Range("N10").End(xlDown)).Copy
                Sheets("Results").Cells(LastRow, "A").PasteSpecial xlAll
                
            'If its not any of the cases listed above it will go trhough every sheet
            Case Else
            'Copy from worksheet from A6 to end of column A (No copying of headers)
            'Paste to Results sheet at the end of existing data
                If Not sh.name Like "s*" Then
                    LastRow = DestSh.Cells(Rows.Count, 1).End(3).Row + 1
                    sh.Range("A6", sh.Range("N10").End(xlDown)).Copy
                    Sheets("Results").Cells(LastRow, "A").PasteSpecial xlAll
                End If
                
            End Select
            
        'goes to next sheet
        Next sh

        .ScreenUpdating = True
        .EnableEvents = True
    End With

End Sub

'Function to check if new destination file exists
Function SheetExists(shName As String) As Boolean

    With ActiveWorkbook
        On Error Resume Next
        SheetExists = (.Sheets(shName).name = shName)
        On Error GoTo 0
    End With

End Function